#pragma kernel FindTransformMask
#pragma kernel FixTransformMask
#pragma kernel TransformDimension
#pragma kernel FindExtendTransformMask
#pragma kernel TransformExtendDimension
#pragma kernel CombineAlphaMask
#pragma kernel ThreeLayersBlend

#include "TerrainUtils.hlsl"

// 三层混合结构分为
// 1、特殊的三层混合结构, 二层转三层
// 2、原始的三层结构
// 满足Mask的条件: 
// 1、二、三层都存在的ID
// 2、TODO: 混合值相似的ID
[numthreads(8, 8, 1)] 
void FindTransformMask (uint3 id : SV_DispatchThreadID)
{
    // R: ID; G: Alpha; B: BlendMask; A: EdgeMask
    float4 sEdgeMask = _TextureInput01[id.xy];
    float4 tEdgeMask = _TextureInput02[id.xy];
    float4 rawIDValue = _TextureInput03[id.xy];
    float4 alphaMask = 0;
    // 处理了ST重合的位置
    if (sEdgeMask.w > 0 && tEdgeMask.w > 0)
    {
        for (uint i = 0; i < 8; i++)
        {
            uint2 nearID = GetNearID(id.xy, i);
            float4 nearSEdgeMask = _TextureInput01[nearID];
            float4 nearTEdgeMask = _TextureInput02[nearID];
            if (nearSEdgeMask.w == 0 || nearTEdgeMask.w == 0) continue;
            if (sEdgeMask.x == nearSEdgeMask.x || tEdgeMask.x == nearTEdgeMask.x) continue;
            if (sEdgeMask.x == nearTEdgeMask.x)
                alphaMask.xy = sEdgeMask.x;
            else if (tEdgeMask.x == nearSEdgeMask.x)
                alphaMask.xy = tEdgeMask.x;
        }
    }
    // 处理第二层未重合的位置(尽量查找)
    else if (sEdgeMask.w > 0)
    {
        for (uint i = 0; i < 8; i++)
        {
            uint2 nearID = GetNearID(id.xy, i);
            float4 nearSEdgeMask = _TextureInput01[nearID];
            float4 nearTEdgeMask = _TextureInput02[nearID];
            if (nearSEdgeMask.w == 0 || sEdgeMask.x == nearSEdgeMask.x) continue;
            if (sEdgeMask.x == nearTEdgeMask.x)
                alphaMask.x = sEdgeMask.x;
            else if (tEdgeMask.x == nearSEdgeMask.x)
                alphaMask.x = tEdgeMask.x;
        }
    }
    // 处理第三层未重合的位置(尽量查找)
    else if (tEdgeMask.w > 0)
    {
        for (uint i = 0; i < 8; i++)
        {
            uint2 nearID = GetNearID(id.xy, i);
            float4 nearSEdgeMask = _TextureInput01[nearID];
            float4 nearTEdgeMask = _TextureInput02[nearID];
            if (nearTEdgeMask.w == 0 || tEdgeMask.x == nearTEdgeMask.x) continue;
            if (sEdgeMask.x == nearTEdgeMask.x)
                alphaMask.y = sEdgeMask.x;
            else if (tEdgeMask.x == nearSEdgeMask.x)
                alphaMask.y = tEdgeMask.x;
        }
    }
    alphaMask.z = alphaMask.x > 0 ? 1 : (sEdgeMask.w > 0 ? 0.5 : 0);
    alphaMask.w = alphaMask.y > 0 ? 1 : (tEdgeMask.w > 0 ? 0.5 : 0);
    Result01[id.xy] = alphaMask;
}

// 修补剩余的的AlphaMask
// 1、如果和G2B的Mask重合, 并且当前ID和周围AlphaMask的ID不匹配, .zw = 0, 匹配就附上值;
// 2、如果和G2B的Mask重合, 周围不存在AlphaMask, .zw = 1; 
[numthreads(8, 8, 1)]
void FixTransformMask (uint3 id : SV_DispatchThreadID)
{
    float4 rawIDValue = _TextureInput01[id.xy];
    float4 alphaMask = _TextureInput02[id.xy];
    if (0 < alphaMask.z && alphaMask.z < 1)
    {
        if (DecodeMask(rawIDValue.r).y == 0)
        {
            bool isIsolate = true;
            for (uint i = 0; i < 8; i++)
            {
                uint2 nearID = GetNearID(id.xy, i);
                float4 nearAlphaMask = _TextureInput02[nearID];
                if (nearAlphaMask.z != 1) continue;
                if (rawIDValue.y != nearAlphaMask.x)
                    isIsolate = false;                    
            }
            if (isIsolate) alphaMask.x = rawIDValue.y;
        }
        else
        {
            bool retainRawID = true;
            for (uint i = 0; i < 8; i++)
            {
                uint2 nearID = GetNearID(id.xy, i);
                float4 nearAlphaMask = _TextureInput02[nearID];
                if (nearAlphaMask.z != 1) continue;

                if (retainRawID)
                {
                    alphaMask.x = nearAlphaMask.x;
                    retainRawID = false;
                }
            }
            if (retainRawID) alphaMask.x = 0;
        }
    }
    if (0 < alphaMask.w && alphaMask.w < 1)
    {
        {
            bool retainRawID = true;
            for (uint i = 0; i < 8; i++)
            {
                uint2 nearID = GetNearID(id.xy, i);
                float4 nearAlphaMask = _TextureInput02[nearID];
                if (nearAlphaMask.w != 1) continue;

                if (retainRawID)
                {
                    alphaMask.y = nearAlphaMask.y;
                    retainRawID = false;
                }
            }
            if (retainRawID) alphaMask.y = 0;
        }
    }
    alphaMask.z = alphaMask.x > 0 ? 1 : (alphaMask.z > 0 ? 0.5 : 0);
    alphaMask.w = alphaMask.y > 0 ? 1 : (alphaMask.w > 0 ? 0.5 : 0);
    Result01[id.xy] = alphaMask;
}

// 根据Alpha转移, 对RawIDEdgeTexture进行操作并输出
// 后续对RawIDEdgeTexture进行扩展, 因为涉及
[numthreads(8, 8, 1)]
void TransformDimension (uint3 id : SV_DispatchThreadID)
{
    // GB Mask ==> 0; 
    float4 sEdgeMask = _TextureInput01[id.xy];
    float4 tEdgeMask = _TextureInput02[id.xy];
    float4 alphaMask = _TextureInput03[id.xy];
    float4 rawIDValue = _TextureInput04[id.xy];
    float4 alphaValue = 0;
    bool changeGChannel = false;
    bool changeBChannel = false;
    if (alphaMask.z == 1 && alphaMask.w == 1)
    {
        if (sEdgeMask.x == alphaMask.x)
        {
            alphaValue.x = sEdgeMask.x;
            changeGChannel = true;
        }
        if (tEdgeMask.x == alphaMask.y)
        {
            alphaValue.y = tEdgeMask.x;
            changeBChannel = true;
        }
    }
    else
    {
        if (alphaMask.z == 1 && sEdgeMask.x == alphaMask.x)
        {
            alphaValue.x = sEdgeMask.x;
            changeGChannel = true;
        }
        else if (alphaMask.w == 1 && tEdgeMask.x == alphaMask.y)
        {
            alphaValue.y = tEdgeMask.x;
            changeBChannel = true;
        }
        else
        {
            for (uint i = 0; i < 8; i++)
            {
                uint2 nearID = GetNearID(id.xy, i);
                float4 nearAlphaMask = _TextureInput03[nearID];
                if (nearAlphaMask.w == 0) continue;
                if (sEdgeMask.x == nearAlphaMask.x)
                {
                    alphaValue.x = sEdgeMask.x;
                    changeGChannel = true;
                }
                if (tEdgeMask.x == nearAlphaMask.y)
                {
                    alphaValue.y = tEdgeMask.x;
                    changeBChannel = true;
                }
            }
        }
    }
    
    if (changeGChannel)
    {
        sEdgeMask = 0;
        rawIDValue.y = Encode(Decode(rawIDValue.y), 0);
    }
    if (changeBChannel)
    {
        tEdgeMask = 0;
        rawIDValue.z = Encode(Decode(rawIDValue.z), 0);
    }
    alphaValue.z = alphaValue.x > 0 ? 1 : 0;
    alphaValue.w = alphaValue.y > 0 ? 1 : 0;

    Result01[id.xy] = sEdgeMask;
    Result02[id.xy] = tEdgeMask;
    Result03[id.xy] = rawIDValue;
    Result04[id.xy] = alphaValue;
}

// TODO: 相交的和AlphaMask并不完全重合, 倒是扩展后的二三层是有重合的
[numthreads(8, 8, 1)]
void FindExtendTransformMask (uint3 id : SV_DispatchThreadID)
{
    // Extend: x y z 当前和周围的ID; w: 范围
    // Alpha Mask: RB(GA): 两层都存在的ID和遮罩
    float4 extendLayerS = _TextureInput01[id.xy];
    float4 extendLayerT = _TextureInput02[id.xy];
    float4 alphaMask = _TextureInput03[id.xy];
    float4 alphaResult = 0;
    bool isTransform = false;

    // 相交部分重合的情况: 对比双方是否有相同ID
    if (extendLayerS.y > 0 && extendLayerT.y > 0)
    {
        if (extendLayerS.x == extendLayerT.x || extendLayerS.x == extendLayerT.y)
            alphaResult.xy = extendLayerS.x;
        else if (extendLayerS.y == extendLayerT.x || extendLayerS.y == extendLayerT.y)
            alphaResult.xy = extendLayerS.y;
    }
    // 寻找隔壁是否存在相同ID, 如果有就记录
    else
    {
        bool notFindS = true;
        bool notFindT = true;
        for (uint i = 0; i < 8; i++)
        {
            uint2 nearID = GetNearID(id.xy, i);
            float4 nearExtendLayerS = _TextureInput01[nearID];
            float4 nearExtendLayerT = _TextureInput02[nearID];
            if (extendLayerS.y > 0 && nearExtendLayerT.y > 0)
            {
                if (extendLayerS.x == nearExtendLayerT.x || extendLayerS.x == nearExtendLayerT.y)
                    alphaResult.x = extendLayerS.x;
                else if (extendLayerS.y == nearExtendLayerT.x || extendLayerS.y == nearExtendLayerT.y)
                    alphaResult.x = extendLayerS.y;
                if (alphaResult.x > 0)
                {
                    notFindS = false;
                    break;
                }
            }
            else if (extendLayerT.y > 0 && nearExtendLayerS.y > 0)
            {
                if (extendLayerT.x == nearExtendLayerS.x || extendLayerT.x == nearExtendLayerS.y)
                    alphaResult.y = extendLayerT.x;
                else if (extendLayerT.y == nearExtendLayerS.x || extendLayerT.y == nearExtendLayerS.y)
                    alphaResult.y = extendLayerT.y;
                if (alphaResult.y > 0)
                {
                    notFindT = false;
                    break;
                }
            }
        }
        if (notFindS && extendLayerS.y > 0 && alphaMask.z == 1)
        {
            if (extendLayerS.x == alphaMask.x)
                alphaResult.x = extendLayerS.x;
            else if (extendLayerS.y == alphaMask.x)
                alphaResult.x = extendLayerS.y;
        }
        else if (notFindT && extendLayerT.y > 0 && alphaMask.w == 1)
        {
            if (extendLayerT.x == alphaMask.y)
                alphaResult.y = extendLayerT.x;
            else if (extendLayerT.y == alphaMask.y)
                alphaResult.y = extendLayerT.y;
        }
        else
        {
            for (uint j = 0; j < 8; j++)
            {
                uint2 nearID = GetNearID(id.xy, j);
                float4 nearAlphaMask = _TextureInput03[nearID];
                if (notFindS && extendLayerS.y > 0 && nearAlphaMask.z == 1)
                {
                    if (extendLayerS.x == nearAlphaMask.x)
                        alphaResult.x = extendLayerS.x;
                    else if (extendLayerS.y == nearAlphaMask.x)
                        alphaResult.x = extendLayerS.y;
                }
                if (notFindT && extendLayerT.y > 0 && nearAlphaMask.w == 1)
                {
                    if (extendLayerT.x == nearAlphaMask.y)
                        alphaResult.y = extendLayerT.x;
                    else if (extendLayerT.y == nearAlphaMask.y)
                        alphaResult.y = extendLayerT.y;
                }
            }
        }
        if (notFindS && extendLayerS.y > 0 && alphaResult.x == 0) alphaResult.x = extendLayerS.x;
        if (notFindT && extendLayerT.y > 0 && alphaResult.y == 0) alphaResult.y = extendLayerT.x;
    }

    alphaResult.z = alphaResult.x > 0 ? 1 : 0;
    alphaResult.w = alphaResult.y > 0 ? 1 : 0;

    Result01[id.xy] = alphaResult;
}

// 更改扩展后相交处的AlphaMask
[numthreads(8, 8, 1)]
void TransformExtendDimension (uint3 id : SV_DispatchThreadID)
{
    // Extend: x y z 当前和周围的ID; w: 范围
    float4 extendLayerS = _TextureInput01[id.xy];
    float4 extendLayerT = _TextureInput02[id.xy];
    float4 alphaMaskExtend = _TextureInput03[id.xy];
    float4 rawIDValue = _TextureInput04[id.xy];
    float4 alphaValue = 0;
    // Alpha Mask 周围的ID Mask => 0
    // Alpha 填补ID Mask修改的位置
    // Extend改为非AlphaMask对应的
    if (alphaMaskExtend.z == 1)
    {
        if (extendLayerS.x == alphaMaskExtend.x)
            extendLayerS.x = extendLayerS.y;
    }
    else if (extendLayerS.w > 0)
    {
        for (uint i = 0; i < 8; i++)
        {
            uint2 nearID = GetNearID(id.xy, i);
            float4 nearAlphaMaskExtend = _TextureInput03[nearID];
            if (nearAlphaMaskExtend.z == 0) continue;
            if (extendLayerS.x == nearAlphaMaskExtend.x)
            {
                alphaValue.x = extendLayerS.x;
                rawIDValue.y = Encode(Decode(rawIDValue.y), 0);
            }
        }
    }

    if (alphaMaskExtend.w == 1)
    {
        if (extendLayerT.x == alphaMaskExtend.y)
            extendLayerT.x = extendLayerT.y;
    }
    else if (extendLayerT.w > 0)
    {
        for (uint i = 0; i < 8; i++)
        {
            uint2 nearID = GetNearID(id.xy, i);
            float4 nearAlphaMaskExtend = _TextureInput03[nearID];
            if (nearAlphaMaskExtend.w == 0) continue;
            if (extendLayerT.x == nearAlphaMaskExtend.y)
            {
                alphaValue.y = extendLayerT.x;
                rawIDValue.z = Encode(Decode(rawIDValue.z), 0);
            }
        }
    }

    alphaValue.z = alphaValue.x > 0 ? 1 : 0;
    alphaValue.w = alphaValue.y > 0 ? 1 : 0;

    Result01[id.xy] = extendLayerS;
    Result02[id.xy] = extendLayerT;
    Result03[id.xy] = rawIDValue;
    Result04[id.xy] = alphaValue;
}

// TODO: 合并AlphaMask
[numthreads(8, 8, 1)]
void CombineAlphaMask (uint3 id : SV_DispatchThreadID)
{
    float4 alphaMaskEdge = _TextureInput01[id.xy];
    float4 alphaMaskExtend = _TextureInput02[id.xy];
    // 累加就行
    float4 alphaValue = 0;
    if (alphaMaskExtend.z == 1)
        alphaValue.x = alphaMaskExtend.x;
    if (alphaMaskExtend.w == 1)
        alphaValue.x = alphaMaskExtend.y;
    if (alphaMaskEdge.z == 1)
        alphaValue.x = alphaMaskEdge.x;
    if (alphaMaskEdge.w == 1)
        alphaValue.x = alphaMaskEdge.y;
    
    alphaValue.z = alphaValue.x > 0 ? 1 : 0;
    Result01[id.xy] = alphaValue;
}

[numthreads(8, 8, 1)]
void ThreeLayersBlend (uint3 id : SV_DispatchThreadID)
{
    float4 secondLayerValueD = _TextureInput01[id.xy];
    float4 secondLayerValueT = _TextureInput02[id.xy];
    float4 thirdLayerValueT = _TextureInput03[id.xy];
    float4 alphaLayerValue = _TextureInput04[id.xy];
      
    float4 threeAreaAlpha = 0;
    uint3 texID = 0;
    if (secondLayerValueD.w != 0)
    {
        texID = uint3(id.xy, Decode(secondLayerValueD.x));
        threeAreaAlpha.x = _AlphaTextureArray[texID];
    }
    if (secondLayerValueT.w != 0)
    {
        texID = uint3(id.xy, Decode(secondLayerValueT.x));
        threeAreaAlpha.y = _AlphaTextureArray[texID];
    }
    if (thirdLayerValueT.w != 0)
    {
        texID = uint3(id.xy, Decode(thirdLayerValueT.x));
        threeAreaAlpha.z = _AlphaTextureArray[texID];
    }
    if (alphaLayerValue.w != 0)
    {
        texID = uint3(id.xy, Decode(alphaLayerValue.x));
        threeAreaAlpha.w = _AlphaTextureArray[texID];
    }
    Result01[id.xy] = threeAreaAlpha;
}